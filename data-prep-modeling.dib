#!markdown

# Predicting restaurant inspection scores in NYC

The dataset represents the NYC restaurant inspection results. For more information, see [DOHMH New York City Restaurant Inspection Results](https://data.cityofnewyork.us/Health/DOHMH-New-York-City-Restaurant-Inspection-Results/43nn-pn8j).

This notebooks uses [.NET for Apache Spark](https://dot.net/spark) & [ML.NET](https://dot.net/ml) to prepare data for modeling and train a machine learning model to predict the score given to an establishment after an inspection.

#!markdown

## Data preparation

#!markdown

### Install & reference Microsoft.Spark package

#!fsharp

#r "nuget:Microsoft.Spark"

#!fsharp

open Microsoft.Spark.Sql

#!markdown

### Initialize SparkSession

#!fsharp

let sparkSession = 
    SparkSession
        .Builder()
        .AppName("on-dotnet-fsharp")
        .GetOrCreate()

#!markdown

### Load data into Spark DataFrame

#!fsharp

let df = 
    sparkSession
        .Read()
        .Option("header","true")
        .Option("inferSchema","true")
        .Csv("data/nyc-restaurant-inspections.csv")

#!fsharp

df.PrintSchema()

#!markdown

### Group data by borough

#!fsharp

let boroughs = 
    df.GroupBy([|Functions.Col("BORO")|]).Count()

#!fsharp

boroughs.Show()

#!markdown

### Remove null or missing boroughs

#!fsharp

let cleanBoroughs = boroughs.Filter(Functions.Col("BORO").NotEqual("0"))

#!fsharp

cleanBoroughs.Show()

#!markdown

### Install & reference Plotly.NET visualization package

#!fsharp

#r "nuget: Plotly.NET, 2.0.0-preview.5"
#r "nuget: Plotly.NET.Interactive, 2.0.0-preview.5"

#!fsharp

open Plotly.NET

#!markdown

### Plot borough information

#!fsharp

let boroughs,counts = 
    cleanBoroughs
      .Select("BORO","count")
      .OrderBy(Functions.Col("count").Desc())
      .Collect()
      |> Seq.map(fun row -> (string row.[0], string row.[1] |> int))
    |> Seq.unzip

let boroughColumn = Chart.Column(boroughs,counts)

#!fsharp

boroughColumn

#!markdown

### Get coordinate information

#!fsharp

let coordinates = 
  df
    .Select(
      Functions.Col("CAMIS"),Functions.Col("Latitude"),Functions.Col("Longitude"))
    .DropDuplicates("CAMIS")

#!fsharp

coordinates.Show()

#!markdown

### Remove null or missing coordinates

#!fsharp

let nonZeroCoordinates = coordinates.Where("Latitude != 0.0 OR Longitude != 0.0")

#!markdown

### Plot coordinates

#!fsharp

let labels, lat, lon = 
    nonZeroCoordinates.Select("CAMIS","Latitude","Longitude").Collect()
    |> Seq.map(fun row -> string row.[0], string row.[1] |> float, string row.[2] |> float)
    |> Seq.unzip3

let pointMapbox = 
    Chart.PointMapbox(
        lon,lat,
        Labels = labels,
        TextPosition = StyleParam.TextPosition.TopCenter
    )
    |> Chart.withMapbox(
        Mapbox.init(
            Style=StyleParam.MapboxStyle.OpenStreetMap,
            Center=(-73.99,40.73),
            Zoom=8.
        )
    )

#!fsharp

pointMapbox

#!markdown

### Query & prepare data for modeling

In this query, the dataset is processed to extract the following columns:

- CAMIS
- BORO
- INSPECTION DATA
- INSPECTION TYPE
- VIOLATION CODE
- CRITICAL FLAG
- SCORE

SCORE is the column to predict when we train our machine learning model. The rest of the columns will be the features or inputs for our model.

#!fsharp

let prepData = 
    df
      .Select("CAMIS","BORO","INSPECTION DATE","INSPECTION TYPE","VIOLATION CODE","CRITICAL FLAG","SCORE")
      .Where(Functions.Col("INSPECTION DATE").NotEqual("01/01/1900"))
      .Where(Functions.Col("SCORE").IsNotNull())
      .Where(Functions.Col("BORO").NotEqual("0"))
      .WithColumn("CRITICAL FLAG",Functions.When(Functions.Col("CRITICAL FLAG").EqualTo("Critical"),1).Otherwise(0))
      .GroupBy("CAMIS","BORO","INSPECTION DATE","INSPECTION TYPE", "SCORE") //"INSPECTION TYPE","VIOLATION CODE"
      .Agg(
          Functions.CollectList(Functions.Col("CRITICAL FLAG")).Alias("VIOLATIONS"),
          Functions.CollectList(Functions.Col("VIOLATION CODE")).Alias("CODES"))
      .WithColumn("CRITICAL VIOLATIONS", Functions.Expr("AGGREGATE(VIOLATIONS, 0, (acc, val) -> acc + val)"))
      .WithColumn("TOTAL VIOLATIONS", Functions.Size(Functions.Col("VIOLATIONS")))
      .WithColumn("CODES",Functions.ArrayJoin(Functions.Col("CODES"),","))
      .Drop("VIOLATIONS","INSPECTION DATE")
      .OrderBy("CAMIS")

#!markdown

### Save data

The results of the query are saved to a directory called *processed-data*

#!fsharp

prepData.Write().Mode(SaveMode.Overwrite).Csv("processed-data")

#!markdown

## Train the model

In this section, we'll take the data that was processed using .NET for Apache Spark and use it for training

#!markdown

### Install ML.NET AutoML packages

#!fsharp

#r "nuget:Microsoft.ML"
#r "nuget:Microsoft.ML.AutoML"

#!fsharp

open Microsoft.ML
open Microsoft.ML.Data
open Microsoft.ML.AutoML

#!markdown

### Define model input and output schema

#!fsharp

[<CLIMutable>]
type ModelInput = {
    [<LoadColumn(0)>] Camis: string
    [<LoadColumn(1)>] Boro: string
    [<LoadColumn(2)>] InspectionType: string
    [<LoadColumn(3)>] InspectionScore: float32
    [<LoadColumn(4)>] Violations: string
    [<LoadColumn(5)>] CriticalViolations: float32
    [<LoadColumn(6)>] TotalViolations: float32
}

[<CLIMutable>]
type ModelOutput = {
    InspectionScore: float32
    Score: float32
}

#!markdown

### Initialize MLContext

#!fsharp

let mlContext = MLContext()

#!markdown

### Load data into IDataView

#!fsharp

let dataPath = Path.Join(__SOURCE_DIRECTORY__,"processed-data\*.csv")
let data = mlContext.Data.LoadFromTextFile<ModelInput>(dataPath,separatorChar=',', allowQuoting=true, hasHeader=false)

#!markdown

## Split into training and test sets

#!fsharp

let dataSplit = mlContext.Data.TrainTestSplit(data, samplingKeyColumnName="Camis")

#!markdown

### Train model

#!fsharp

let experimentTimeInSeconds = 300u
let labelColumnName = "InspectionScore"
let experimentResult = mlContext.Auto().CreateRegressionExperiment(experimentTimeInSeconds).Execute(dataSplit.TrainSet, labelColumnName)

#!markdown

### Evaluate the model

#!fsharp

let bestModel = experimentResult.BestRun.Model

#!fsharp

let predictions = bestModel.Transform dataSplit.TestSet
let metrics = mlContext.Regression.Evaluate(predictions,"InspectionScore","Score")

metrics

#!markdown

### Make predictions with the model

#!fsharp

let input = {
    Camis="41720083"
    Boro="Manhattan"
    InspectionType="Cycle Inspection / Re-inspection"
    InspectionScore=11.0f
    Violations= "04H,09C,10F"
    CriticalViolations=1.0f
    TotalViolations=3.0f
}

let predictionEngine= mlContext.Model.CreatePredictionEngine<ModelInput,ModelOutput> bestModel

let prediction = predictionEngine.Predict(input)

prediction

#!markdown

### Save the model

#!fsharp

mlContext.Model.Save(bestModel, data.Schema,"InspectionModel.zip")
